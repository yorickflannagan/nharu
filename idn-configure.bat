@ECHO off
REM * * * * * * * * * * * * * * * * * * * * * * * * *
REM GNU IDN Library for Windows configuration script
REM Copyleft 2016 by The Crypthing Initiative
REM * * * * * * * * * * * * * * * * * * * * * * * * *
REM Make sure the following steps were done prior to run this script:
REM 1. Install GnuWin (http://gnuwin32.sourceforge.net/)
REM 2. Install ActivePearl (http://www.activestate.com/activeperl)
REM 3. Install Windows SDK (at least version 7.1)
REM 4. Clone git://git.savannah.gnu.org/libidn.git and checkout libidn-1-11
REM 5. Execute under SDK command line environment
REM * * * * * * * * * * * * * * * * * * * * * * * * *
SETLOCAL EnableExtensions
SETLOCAL EnableDelayedExpansion

:CMD_LINE
SET ME=%~n0
SET CUR=%~dp0
SET _PREFIX_=%CUR%3rdparty\idn
SET _CVARSDLL_=-DWIN32 -D_USRDLL -DIDNA_EXPORTS -DHAVE_CONFIG_H -D_CRT_SECURE_NO_DEPRECATE -D_CRT_NONSTDC_NO_DEPRECATE -D_MBCS
SET _CDEBUG_=/GL /analyze-
SET _CFLAGS_=/Zc:wchar_t /Gm- /WX- /Gd
SET _LFLAGS_=/LTCG /NOLOGO
FOR %%a IN (%*) DO (
    CALL:GET_ARGS "--","%%a" 
)
IF DEFINED --help (
	GOTO USAGE
)
IF DEFINED --prefix (
	SET _PREFIX_=%--prefix%
)
IF DEFINED --cvarsdll (
	SET _CVARSDLL_=%--cvarsdll%
)
IF DEFINED --cdebug (
	SET _CDEBUG_= %--cdebug%
)
IF DEFINED --cflags (
	SET _CFLAGS_=%--cflags%
)
IF DEFINED --lflags (
	SET _LFLAGS_=%--lflags%
)
REM Required by GNU mkdir
SET _PREFIX_=%_PREFIX_:\=\/%

:VARS
IF NOT EXIST libidn.mak.in (
	ECHO %ME%: Makefile for GNU IDN Library not found
	EXIT /B 1
)
find . -name libidn.sln -printf "%%T@ %%p\n" 2>nul | sort -n | tail -1 | cut -f2- -d" " 1>tmp.tmp
FOR /F %%a IN (tmp.tmp) DO SET _PACKAGE_=%%a
DEL tmp.tmp
IF "%_PACKAGE_%" EQU "" (
	ECHO %ME%: GNU IDN Library not found
	EXIT /B 1
)
FOR /F %%a IN ('dirname %_PACKAGE_%') DO SET _PACKAGE_=%%a
FOR /F %%a IN ('dirname %_PACKAGE_%') DO SET _PACKAGE_=%%a
REM CL does not support INCLUDE with a list of directories; so we must split them to compiler
REM This is not well documented: https://msdn.microsoft.com/en-us/library/kezkeayy.aspx
SET _SDK_INCLUDE_=/I"%INCLUDE:;=" /I"%"
SET _SDK_INCLUDE_=%_SDK_INCLUDE_:/I""=%

:CLEANUP
IF EXIST libidn.mak (
	nmake /NOLOGO NODEBUG=1 /f libidn.mak clean
	IF %ERRORLEVEL% NEQ 0 (
		ECHO %ME%: Could not cleanup existing configuration
		EXIT /B %ERRORLEVEL%
	)
	rm libidn.mak
	IF %ERRORLEVEL% NEQ 0 (
		ECHO %ME%: Could not cleanup existing configuration
		EXIT /B %ERRORLEVEL%
	)
)

:GENRFC
IF NOT EXIST "%_PACKAGE_%/lib/rfc3454.c" (
	CD %_PACKAGE_%/lib
	perl gen-stringprep-tables.pl ../doc/specifications/rfc3454.txt
	IF %ERRORLEVEL% NEQ 0 (
		ECHO %ME%: Could not generate rfc3454.c file
		CD %CUR%
		EXIT /B %ERRORLEVEL%
	)
	CD %CUR%
)

:GENMAK
ECHO # * * * * * * * * * * * * * * * * * * * * * * * * * *> libidn.mak
ECHO # Copyleft 2016 by The Crypthing Initiative>> libidn.mak
ECHO # Makefile generated by %ME%>> libidn.mak
ECHO # Do not edit it, unless you know what you are doing>> libidn.mak
ECHO # * * * * * * * * * * * * * * * * * * * * * * * * * *>> libidn.mak
FOR /F "tokens=* delims=," %%i IN (libidn.mak.in) DO (
	SET LINE=%%i
	SET LINE=!LINE:_PREFIX_=%_PREFIX_%!
	SET LINE=!LINE:_CVARSDLL_=%_CVARSDLL_%!
	SET LINE=!LINE:_CDEBUG_=%_CDEBUG_%!
	SET LINE=!LINE:_CFLAGS_=%_CFLAGS_%!
	SET LINE=!LINE:_LFLAGS_=%_LFLAGS_%!
	SET LINE=!LINE:_PACKAGE_=%_PACKAGE_%!
	SET LINE=!LINE:_SDK_INCLUDE_=%_SDK_INCLUDE_%!
	ECHO !LINE!>> libidn.mak
)
GOTO DONE

:GET_ARGS
ECHO.%~2 | FINDSTR /C:"%~1" 1>nul
IF NOT errorlevel 1 (
	SET KEY=%~2
) ELSE (
	SET VALUE=%~2
)
IF DEFINED KEY (
	SET %KEY%=%~2
)
IF DEFINED VALUE (
	IF DEFINED KEY (
		SET %KEY%=%~2
	)
	SET KEY=
	SET VALUE=
)
GOTO:EOF

:USAGE
ECHO.
ECHO * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ECHO Usage: %ME% [options]
ECHO * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ECHO --prefix: install base directory. Default value: 3rdparty\idn.  Two directories
ECHO    are created under this one: include, to use with /I compiler option and lib, 
ECHO    to use with /LIBPATH linker option.
ECHO --cvarsdll: compiler variables definitions by -D. Default value: -DWIN32
ECHO    -D_USRDLL -DIDNA_EXPORTS -DHAVE_CONFIG_H -D_CRT_SECURE_NO_DEPRECATE
ECHO    -D_CRT_NONSTDC_NO_DEPRECATE -D_MBCS
ECHO --cdebug: compiler debug options. Default value: /GL /analyze-
ECHO --cflags: other compiler flags. Default value: /Zc:wchar_t /Gm- /WX- /Gd
ECHO --lflags: linker flags. Default value: /LTCG /NOLOGO
ECHO.
ECHO Note that CL and LIB options above are added to those defined in win32.mak.
ECHO.
:DONE
ENDLOCAL
