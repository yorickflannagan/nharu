@ECHO OFF

:: ---------------------------------------------------
:: Generated by configure.vbs - EDIT AT YOUR OWN RISK!
:: ---------------------------------------------------

ECHO.
ECHO  * * * * * * * * * * * * * * * * * * * * * * * *
ECHO  OpenSSL Library
ECHO  Build under Windows
ECHO  -----------------------------------------------
ECHO  Copyleft (C) 2015 by The Crypthing Initiative
ECHO  Authors:
ECHO    diego.sohsten@caixa.gov.br
ECHO    yorick.flannagan@gmail.com
ECHO.
ECHO  Optional command line arguments:
ECHO     --target=[_TARGET] base target install directory. Default: _HOME\dist
ECHO     --platform=[_PLATFORM]: target platform. Default: Win32
ECHO.
ECHO  * * * * * * * * * * * * * * * * * * * * * * * *
ECHO.

:: Initialization
SET _CUR=%~dp0
SET _VCENV=vcvarsamd64_x86.bat
SET _CFG_TYPE=VC-WIN32
FOR %%i IN ("%~dp0..") DO SET "_HOME=%%~fi"
SET _SOURCE=%_CUR%openssl
FOR %%a IN (%*) DO (CALL:GET_ARGS "--","%%a")
VERIFY >NUL
IF DEFINED --target   (SET _TARGET=%--target%)
IF DEFINED --platform (SET _PLATFORM=%--platform%)
IF "%_PLATFORM%" EQU "" (SET _PLATFORM=Win32)
IF "%_TARGET%" EQU "" (SET _INSTALL=%_HOME%\dist\%_PLATFORM%\ssl) ELSE (SET _INSTALL=%_TARGET%\%_PLATFORM%\ssl)
IF "%_PLATFORM%" EQU "x64" (
	SET _VCENV=vcvars64.bat
	SET _CFG_TYPE=VC-WIN64A
)

:: Generated by Configure
SET _NASM_INSTALL_PATH=__NASM_INSTALL_PATH__
SET _VS_INSTALL_PATH=__VS_INSTALL_PATH__

:: Display values
ECHO.
ECHO  -----------------------------------------------
ECHO  OpenSSL Windows configuration environment
ECHO  -----------------------------------------------
ECHO.
ECHO  Source files location:    %_SOURCE%
ECHO  Target install directory: %_INSTALL%
ECHO  Compilation environment:  %_VS_INSTALL_PATH%VC\Auxiliary\Build\%_VCENV%
ECHO  NASM location:            %_NASM_INSTALL_PATH%
ECHO  Configuration command:    perl.exe Configure %_CFG_TYPE% no-shared --prefix="%_INSTALL%" --openssldir="%_INSTALL%"
ECHO.

:: Build
CALL "%_VS_INSTALL_PATH%VC\Auxiliary\Build\%_VCENV%"
SET PATH=%_NASM_INSTALL_PATH%;%PATH%
ECHO.
ECHO  -----------------------------------------------
ECHO  Configuring OpenSSL
ECHO  -----------------------------------------------
ECHO. 
CD "%_SOURCE%"
perl.exe Configure %_CFG_TYPE% no-shared --prefix="%_INSTALL%" --openssldir="%_INSTALL%"
IF %ERRORLEVEL% NEQ 0 GOTO :END
ECHO.
ECHO  -----------------------------------------------
ECHO  Adjusting OpenSSL makefile
ECHO  -----------------------------------------------
ECHO. 
powershell -NoLogo -NoProfile -NonInteractive -Command "(Get-Content makefile) -replace '/MT', '/MD' | Out-File makefile"
IF %ERRORLEVEL% NEQ 0 GOTO :END
ECHO.
ECHO  -----------------------------------------------
ECHO  Building OpenSSL
ECHO  -----------------------------------------------
ECHO. 
CALL NMAKE /NOLOGO
IF %ERRORLEVEL% NEQ 0 GOTO :END
ECHO.
ECHO  -----------------------------------------------
ECHO  Testing OpenSSL
ECHO  -----------------------------------------------
ECHO. 
CALL NMAKE /NOLOGO TEST
IF %ERRORLEVEL% NEQ 0 GOTO :END
ECHO.
ECHO  -----------------------------------------------
ECHO  Installing OpenSSL
ECHO  -----------------------------------------------
ECHO. 
CALL NMAKE /NOLOGO INSTALL
IF %ERRORLEVEL% NEQ 0 GOTO :END
ECHO.
ECHO  -----------------------------------------------
ECHO  Clean-up OpenSSL
ECHO  -----------------------------------------------
ECHO. 
CALL NMAKE /NOLOGO CLEAN
IF %ERRORLEVEL% NEQ 0 GOTO :END
ECHO.
ECHO  -----------------------------------------------
ECHO  All tasks are done
ECHO  -----------------------------------------------
ECHO. 

GOTO:END

:GET_ARGS
:: PROCESS COMAND LINE ARGUMENT OF TYPE --arg=value
:: %~1: ARGUMENT MARKER (USUALY --)
:: %~2: OUT
ECHO.%~2 | FINDSTR /C:"%~1" 1>nul
IF NOT errorlevel 1 (
	SET __KEY=%~2
) ELSE (
	SET __VALUE=%~2
)
IF DEFINED __KEY (
	SET %__KEY%=%~2
)
IF DEFINED __VALUE (
	IF DEFINED __KEY (
		SET %__KEY%=%~2
	)
	SET __KEY=
	SET __VALUE=
)
GOTO:EOF

:: Clean-up
:END
SET _CUR=
SET _TARGET=
SET _PLATFORM=
SET _HOME=
SET _NASM_INSTALL_PATH=
SET _VS_INSTALL_PATH=
SET _SOURCE=
SET _VCENV=
SET _CFG_TYPE=
SET _INSTALL=
SET --target=
SET --platform=